{$CLEO .s}
//-------------MAIN---------------
0000: NOP
thread "TDRIFT"

while true
    wait 0
    10@ = 0
    if $ONMISSION == 0
    jf continue
    
    0AB4: 0@ = var 0
    0AB4: 11@ = var 11
    call_scm_func @get_toggle 1 2 1@
    if and
        0@ == 0
        1@ == 0
        11@ == 1
    then
        32@ = 0
        repeat
            wait 0
            0AB4: 0@ = var 0
            0AB4: 11@ = var 11
            call_scm_func @get_toggle 1 2 1@
            if or
                8039: 0@ == 0
                8039: 1@ == 0
                8039: 11@ == 1
                $ONMISSION == 1
            then
                10@ = -1
                break
            end
            
            if 0ADC: test_cheat "DRIFT"
            then
                jump @debug_point   // force drift
            end
            {
            if 0ADC: test_cheat "DRIFM"
            then
                0AB6: store_target_marker_coords_to 23@ 24@ 25@
                0AB3: var 30 = 23@ // X
                0AB3: var 31 = 24@ // Y
                0AB3: var 32 = -100.0 // Z
                0AB3: var 33 = 0  // INT
                0AB3: var 34 = 0.0 // ANG
                0AB3: var 29 = 0 //Fast return disabled
            end  }
            
        until 32@ > 90000
        if 10@ == -1
        then
            10@ = 0
            continue
        end
        
        0209: 17@ = random_int_in_ranges 0 3 // random chance
        if 17@ == 0
        jf continue
        
        0AB4: 17@ = var 10  // energy check
        if 17@ >= 11
        jf continue

        :debug_point

        gosub @rand
        
        //:debug_point2

        0AB4: 11@ = var 19
        if 11@ == 1
        then
            repeat
                wait 0
                0AB4: 11@ = var 19
            until 11@ == 0
        end

        0AB3: var 28 = 1
        
        0AB4: 11@ = var 12
        if 11@ == 1
        then
            0A92: create_custom_thread "cloak.s"
            wait 2500
        end
        
        call_scm_func @get_toggle 1 1 10@
        call_scm_func @get_toggle 1 3 11@
        call_scm_func @get_toggle 1 4 12@
        0AB3: var 44 = 0
        if 12@ == 1
        then
            call_scm_func @move_toggle 2 4 0
            wait 500
        end
        
        if 11@ == 0
        then
            call_scm_func @move_toggle 2 3 1
        end

        if 10@ == 0
        then
            call_scm_func @move_toggle 2 1 1
        end
        
        wait 1000
        0AB3: var 28 = 1
        03E5: show_text_box 'DRIFTD'  //TARDIS has drifted!
        wait 5000
        0AB3: var 28 = 0
        repeat
            wait 0
            0AB4: 0@ = var 0
        until 0@ == 0
        wait 1000
        
        if and 
            $93 == 0
            $94 == 0
        then
            03E5: show_text_box 'DRIFTM'  //Use Sonic Screwdriver to scan and locate Tardis's new position
            0AB3: var 0 = 12
            //02A7: 30@ = create_icon_marker_and_sphere 42 at 23@ 24@ -1000.0
            if
                8202: NOT actor $PLAYER_ACTOR near_car $111 radius 150.0 150.0 flag 0
            then
                run "exterior_calibration.s" 23@ 24@ 25@   
            end
            repeat
                wait 0
                if 20@ == 1 // if need to show T
                then
                    //02A7: 30@ = create_icon_marker_and_sphere 42 at 23@ 24@ -1000.0
                    02A8: 30@ = create_marker 42 at 23@ 24@ -1000.0 
                    20@ = 2 // T is showed
                end    
            until 0202: actor $PLAYER_ACTOR near_car $111 radius 100.0 100.0 flag 0
            0AB3: var 0 = 0
            03E5: show_text_box 'DRIFTF'  //TARDIS is available
            20@ = 0 // T is hided
            if 075C:   marker 30@ enabled
            then 
                Marker.Disable(30@)
            end
        end
    end
end

end_custom_thread


:rand
    0208: 23@ = random_float_in_ranges -2999.9 2999.9
    0208: 24@ = random_float_in_ranges -2999.9 2999.9
    0208: 25@ = random_float_in_ranges -99.9 99.9
    0AB3: var 30 = 23@ // X
    0AB3: var 31 = 24@ // Y
    0AB3: var 32 = -100.0 // Z
    0AB3: var 33 = 0  // INT
    0AB3: var 34 = 0.0 // ANG
    0AB3: var 29 = 0 //Fast return disabled
return
{$INCLUDE move_set_get}