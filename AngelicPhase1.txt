//written by a Weeping Angel
{$CLEO .cs}

//-------------MAIN---------------
thread "AngelicPhase1" 
model.load(18803)
model.load(18812)
model.load(2927)
model.load(2606)
model.load(#CELLPHONE)
0AAC: 16@ = load_audiostream "DWS\AngelicSFX\angelic_DoorCL.mp3" //IF and SET
0AAC: 17@ = load_audiostream "DWS\AngelicSFX\angelic_Beep.mp3" //IF and SET
0AAC: 18@ = load_audiostream "DWS\AngelicSFX\angelic_BlackOut.wav" //IF and SET
0AC1: 19@ = load_audiostream_with_3d_support "DWS\AngelicSFX\angelic_Cracking.mp3" //IF and SET
0AC2: set_3d_audiostream 19@ position 2531.4534 -1285.8961 1054.6406
0AAC: 20@ = load_audiostream "DWS\AngelicSFX\angelic_DoorOP.mp3" //IF and SET
0AC1: 21@ = load_audiostream_with_3d_support "DWS\AngelicSFX\angelic_Giggles.mp3" //IF and SET
0AC2: set_3d_audiostream 21@ position 2560.9209 -1294.7546 1054.6406
0AC1: 22@ = load_audiostream_with_3d_support "DWS\AngelicSFX\angelic_Knocks.mp3" //IF and SET
0AAC: 23@ = load_audiostream "DWS\AngelicSFX\angelic_Ring.mp3" //IF and SET
0AAC: 24@ = load_audiostream "DWS\AngelicSFX\angelic_Flicker.wav" //IF and SET
0AAC: 25@ = load_audiostream "DWS\AngelicSFX\angelic_Switch.mp3" //IF and SET
0AAC: 27@ = load_audiostream "DWS\AngelicSFX\angelic_Scare.wav" //IF and SET
0AAC: 30@ = load_audiostream "DWS\AngelicSFX\angelic_Ambience.mp3" //IF and SET
3@ = 1 //toggle follow
4@ = 1 //toggle scare1
7@ = 1 //toggle switch
11@ = 1 //toggle scare2
13@ = 0 //counter
26@ = 0 //3Dsfxflag
28@ = 0 //3Dsfxflag2
29@ = 0 //angel spawned

:Angel_0
wait 0 
if
   0AB0:  key_pressed 8
else_jump @Angel_0
04BB: select_interior 2
0840: link_car $111 to_interior 2
00AB: put_car $111 at 2550.7527 -1294.0162 1058.8
0860: link_actor $PLAYER_ACTOR to_interior 2
00A1: put_actor $PLAYER_ACTOR at 2551.0046 -1292.1531 1061.0
01B9: set_actor $PLAYER_ACTOR armed_weapon_to 0 
0992: set_player $PLAYER_CHAR weapons_scrollable 0 
0924: enable_screen_darkness 1 with_value 127
00C0: set_current_time_hours_to 0 minutes_to 0 
0581: enable_radar 0 
0826: enable_hud 0
//Camera.OnPed($PLAYER_ACTOR, 45, 2)
0ACE: show_formatted_text_box "Investigate Factory" 
audiostream.PerformAction(30@, play)
0ABC: set_audiostream 30@ volume 0.1
0AC0: loop_audiostream 30@ flag 1
jump @Angel_1

//------------------------Phase 1-------------------------
:Angel_1
wait 0
if 
    00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2551.2192 -1288.01 1060.9844 radius 2.0 2.0 1.0 on_foot
then 
    object.create(2@, 2927,  2551.0046 -1290.5 1063.0) //gatehole behind player is immediately blocked once he steps outside
    audiostream.PerformAction(16@, play)
    wait 100 
    repeat
        wait 10
        034E: move_object 2@ to 2551.0046 -1290.5 1059.75 speed 0.0 0.0 0.05 flag 0 
        13@ += 5
    until 13@ == 200
    Camera.OnPed($PLAYER_ACTOR, 45, 2)
    00C0: set_current_time_hours_to 0 minutes_to 0
    01B9: set_actor $PLAYER_ACTOR armed_weapon_to 0 
    0992: set_player $PLAYER_CHAR weapons_scrollable 0
    0924: enable_screen_darkness 1 with_value 127
    object.Create(0@, 18803, 2577.75 -1284.5 1064.5) //jumpscare angel 1
    object.Create(12@, 18812, 2579.25 -1300.3 1060.0) //jumpscare angel 2
    0177: set_object 0@ Z_angle_to 90.0
    0177: set_object 12@ Z_angle_to 270.0
    object.Create(8@, 2927, 2522.25 -1301.9027 1047.5)  //block exit
    0177: set_object 8@ Z_angle_to 90.0 
    object.Create(9@, 2927, 2561.2676 -1290.25 1054.0)  //block party1
    object.Create(10@, 2927, 2561.0645 -1298.5 1054.0) //block party2
    object.create(14@, 2606, 2521.1, -1282.9, 1054.4) //switch
    0177: set_object 14@ Z_angle_to 180.0
    0750: set_object 8@ visibility 0 
    0750: set_object 9@ visibility 0 
    0750: set_object 10@ visibility 0 
else
    jump @Angel_1
end 
wait 0
0188: 15@ = create_marker_above_object 14@
wait 0
jump @Angel_Loop_1

:Angel_Loop_1
wait 0
while True
    wait 0
    if 11@ == 1
    then
        09E5: create_flash_light_at 2579.0 -1300.3 1060.0 RGB_mask 211 84 0 radius 10.0
    end
    wait 0
    if 4@ == 1
    then
        09E5: create_flash_light_at 2577.75 -1284.5 1064.5 RGB_mask 211 84 0 radius 10.0
    end
    wait 0 
    if
        00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2530.2834 -1285.8295 1054.6406 radius 2.0 2.0 1.0 on_foot
    then
        jump @Angel_Follow
    else
        if or
            00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2562.8691 -1281.8976 1065.3672 radius 2.0 2.0 1.0 on_foot
            00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2575.9963 -1287.3893 1065.3672 radius 2.0 2.0 1.0 on_foot
        then 
            jump @Angel_Flicker
        else
            if and
                00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2520.9175 -1282.1276 1054.6406 radius 1.0 1.0 1.0 on_foot
                7@ == 1
            then
                0ACE: show_formatted_text_box "Press E to toggle switch"
                jump @Gate_Switch
            else
                if and
                   00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2551.2192 -1288.01 1060.9844 radius 2.25 2.25 1.0 on_foot
                   7@ == 0
                then
                    object.Destroy(8@)
                    object.Destroy(0@)
                    Player.CanMove($PLAYER_CHAR, 0)
                    00A0: store_actor $PLAYER_ACTOR position_to 0@ 12@ 8@
                    12@ += 1.25
                    8@ += 0.75
                    015F: set_camera_position 0@ 12@ 8@ rotation 0.0 0.0 0.0
                    0160: set_camera_point_at 2550.7527 -1294.0162 1060.5 mode 2
                    0AD1: show_formatted_text_highpriority "I am pretty sure she was just right there a minute ago..." time 3000 
                    wait 3000
                    0AAA: 31@ = thread 'ANG1' pointer
                    0D2E: set_thread 31@ var 8 to 3 
                    audiostream.PerformAction(18@, play)
                    0924: enable_screen_darkness 1 with_value 255
                    0AAD: set_audiostream 22@ perform_action 0
                    0AAD: set_audiostream 30@ perform_action 0
                    //12@ += 1.5
                    //run "angel.cs" 1 0@ 12@ 8@ 0.0 40.0 1 0
                    0A92: create_custom_thread "AngelicPhase2.s"
                    0A93: end_custom_thread
                else
                    if 00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2579.0 -1300.3 1060.0 radius 1.3 1.3 1.0 on_foot
                    then
                        jump @Angel_Flicker2
                    else
                        if and
                            00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2538.9827 -1302.955 1054.6406 radius 7.0 7.0 2.0 on_foot
                            26@ == 0
                        then
                            audiostream.PerformAction(19@, play)
                            26@ = 1
                        else
                            if and
                                00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2577.5679 -1284.8757 1054.6406 radius 3.0 3.0 2.0 on_foot
                                28@ == 0
                            then
                                audiostream.PerformAction(21@, play)
                                28@ = 1
                                0AD1: show_formatted_text_highpriority "There has to be a control switch somewhere." time 5000
                            else
                                if and
                                    00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 2525.1128 -1285.9614 1054.6406 radius 2.0 2.0 1.0 on_foot
                                    7@ == 0
                                    29@ == 0
                                then
                                    0AD1: show_formatted_text_highpriority "Something is off." time 2000
                                    run "angel.cs" 1 2518.9153 -1285.8545 1054.6406 0.0 40.0 1 0
                                    29@ = 1
                                    repeat
                                        wait 0
                                    until 80FF: not actor $PLAYER_ACTOR sphere 0 in_sphere 2525.1128 -1285.9614 1054.6406 radius 2.0 2.0 1.0 on_foot
                                    0AAD: set_audiostream 22@ perform_action 1
                                    0AC0: loop_audiostream 22@ flag 1
                                end
                            end
                        end
                    end
                end  
            end 
        end
    end 
end   

:Angel_Follow
wait 0
if 3@ == 1
then
    0209: 5@ = random_int_in_ranges 0 2
    if 5@ == 0
    then 
        object.Create(1@, 18803, 2528.0 -1288.5 1053.75) //the angel that will follow player but not kill him
        0177: set_object 1@ Z_angle_to 315.0
    else
        object.Create(1@, 18803, 2528.0 -1283.5 1053.75)
        0177: set_object 1@ Z_angle_to 225.0
    end 
    3@ = 0
end
jump @Angel_Loop_1

:Angel_Flicker
wait 0
if 4@ == 1
then
    audiostream.PerformAction(24@, play)
    0924: enable_screen_darkness 1 with_value 255
    wait 200
    0924: enable_screen_darkness 1 with_value 127
    wait 250
    0924: enable_screen_darkness 1 with_value 255
    wait 50
    0924: enable_screen_darkness 1 with_value 127
    wait 100
    0924: enable_screen_darkness 1 with_value 255
    object.Destroy(0@)
    wait 50
    0924: enable_screen_darkness 1 with_value 127
    wait 100
    0924: enable_screen_darkness 1 with_value 255
    wait 50
    0924: enable_screen_darkness 1 with_value 127
    4@ = 0
end
jump @Angel_Loop_1

:Angel_Flicker2
wait 0
if 11@ == 1
then
    audiostream.PerformAction(27@, play)
    0924: enable_screen_darkness 1 with_value 255
    wait 100
    0924: enable_screen_darkness 1 with_value 127
    object.Destroy(12@)
    11@ = 0
end
jump @Angel_Loop_1

:Gate_Switch
wait 0
repeat
    wait 0
    if and
        00FF:  actor $PLAYER_ACTOR sphere 0 in_sphere 2520.9175 -1282.1276 1054.6406 radius 1.0 1.0 1.0 on_foot
        0AB0:  key_pressed 69
        7@ == 1
    then 
        audiostream.PerformAction(17@, play)
        wait 1000
        audiostream.PerformAction(25@, play)
        wait 500
        audiostream.PerformAction(20@, play)
        0ABC: set_audiostream 20@ volume 0.2
        object.Destroy(1@)
        13@ = 0
        Player.CanMove($PLAYER_CHAR, 0)
        repeat
            wait 10
            034E: move_object 2@ to 2551.0046 -1290.5 1064.25 speed 0.0 0.0 0.05 flag 0 
            13@ += 5
        until 13@ == 500
        0164: disable_marker 15@
        00AB: put_car $111 at 2527.0342 -1319.1777 1031.4219
        7@ = 0
        wait 1500
        Camera.Restore_WithJumpCut()
        audiostream.PerformAction(23@, play)
        wait 3000
        0729: AS_actor $PLAYER_ACTOR hold_cellphone 1 
        wait 2000
        0AD1: show_formatted_text_highpriority "Hello?" time 2000
        wait 3000
        0AD1: show_formatted_text_highpriority "~b~Help... I can't hold them much longer." time 4000
        wait 5000
        0AD1: show_formatted_text_highpriority "I am the Doctor, and I am here to save you!" time 3000
        wait 5000
        0AD1: show_formatted_text_highpriority "~b~They are coming, there is no time, just remember-" time 3000
        wait 4000
        0AD1: show_formatted_text_highpriority "~b~Don't..." time 2000
        wait 2000
        0AD1: show_formatted_text_highpriority "~b~Blink..." time 2000
        wait 2000
        0729: AS_actor $PLAYER_ACTOR hold_cellphone 0
        wait 1500
        Player.CanMove($PLAYER_CHAR, 1)
        Camera.OnPed($PLAYER_ACTOR, 45, 2)
        0AD1: show_formatted_text_highpriority "I need to return to the Tardis fast so I can help him." time 4000
        0209: 5@ = random_int_in_ranges 0 2
        if 5@ == 0
        then
            0AC2: set_3d_audiostream 22@ position 2561.2676 -1290.25 1054.0
        else
            0AC2: set_3d_audiostream 22@ position 2561.0645 -1298.5 1054.0
        end
        jump @Angel_Loop_1
    end
until 80FF: not actor $PLAYER_ACTOR sphere 0 in_sphere 2520.9175 -1282.1276 1054.6406 radius 2.0 2.0 1.0 on_foot
jump @Angel_Loop_1