
{$CLEO}

{
    Mod Name:   Player Walkstyle Change
    Author:     Wesser
    Credits:    Samp-team
}

const
   GROUP1_NUM       = 1@ 
   GROUP2_NUM       = 2@ 
   GROUP3_NUM       = 3@ 
   PANEL_NUM        = 4@ 
   MY_PANEL         = 5@ 
   CURR_ROW_W       = 6@ 
   GROUP_NUM        = 7@ 
   PANEL_MAX        = 8@ 
   CURR_ROW         = 9@ 
   PANEL_NEXT       = 10@ 
///--------------------------
   PANEL_ITEM       = s$10948 
   PANEL_I_ARR      = $10948 
   PANEL_ITEM_W     = v$10970 
   PANEL_W_ARR      = $10970 
   PANEL_ITEM_SEL   = v$11014 
///--------------------------
   IS_CHEAT_TYPED   = 11@ 
   IS_ENTER_PRESSED = 12@ 
end

var
   PANEL_I_ARR: array 11 of ShortString
   PANEL_W_ARR: array 11 of LongString
end

IS_CHEAT_TYPED = FALSE 
IS_ENTER_PRESSED = FALSE 

GROUP1_NUM = 12 
GROUP2_NUM = 9 
GROUP3_NUM = 1 
PANEL_NUM = -1 
06D1: PANEL_ITEM_SEL = "PLAYER" 

0000: Starting code...

gosub @ScriptLock 

while true
    wait 0 
    if 
        0 > PANEL_NUM 
    then
        if  and
        0256:   player $PLAYER_CHAR defined
        810F:   not player $PLAYER_CHAR wanted_level > 0 
        then
            gosub @CheatTypedCheck 
            if 
                IS_CHEAT_TYPED == TRUE 
            then
                PANEL_NUM = 0 
            end
        end
    else
        if 
            PANEL_NUM == 0 
        then
            0581: enable_radar 0 
            01B4: set_player $PLAYER_CHAR can_move 0 
            08DA: remove_panel MY_PANEL 
            03E6: remove_text_box
            gosub @DrawPanelMain 
            gosub @GetPanelKeys 
            if 
                PANEL_NUM < 0 
            then
                gosub @RemovePanel 
            else
                08D7: CURR_ROW_W = panel MY_PANEL active_row
            end
        end
        if 
            PANEL_NUM > 0 
        then
            if 
                CURR_ROW_W == 0 
            then
                0085: GROUP_NUM = GROUP1_NUM 
                if 
                    PANEL_NUM == 1 
                then
                    gosub @PanelGroup11 
                end
                if 
                    PANEL_NUM == 2 
                then
                    gosub @PanelGroup12 
                end
            end
            if 
                CURR_ROW_W == 1 
            then
                0085: GROUP_NUM = GROUP2_NUM 
                gosub @PanelGroup21 
            end
            if 
                CURR_ROW_W == 2 
            then
                0085: GROUP_NUM = GROUP3_NUM 
                gosub @PanelGroup31 
            end
            gosub @PanelCounter 
            08DA: remove_panel MY_PANEL 
            gosub @DrawPanelNext 
            for 0@ = 0 to 10 
                if 
                08F9:   PANEL_ITEM_SEL == PANEL_W_ARR[0@]
                then
                    08D9: set_panel MY_PANEL row 0@ enable 0 
                    break
                end
            end
            gosub @GetPanelKeys 
            08D7: CURR_ROW = panel MY_PANEL active_row
            if 
                CURR_ROW == 11 
            then
                if 
                85AE:   not PANEL_I_ARR[CURR_ROW] == 'DUMMY' 
                then
                    0085: PANEL_NEXT = PANEL_MAX 
                    PANEL_NEXT += 1 
                    if 
                    003B:   PANEL_NEXT == PANEL_NUM 
                    then
                        PANEL_NUM = 1 
                    end
                end
            else
                if 
                    IS_ENTER_PRESSED == FALSE 
                then
                    if 
                    05AE: PANEL_W_ARR[CURR_ROW] == "PLAYER" 
                    then
                        gosub @ReadFromMemory 
                    else
                        gosub @ReadFromPedsIde 
                        0245: set_actor $PLAYER_ACTOR walk_style_to PANEL_W_ARR[CURR_ROW]
                    end
                    06D1: PANEL_ITEM_SEL = PANEL_W_ARR[CURR_ROW]
                    gosub @RemovePanel 
                    PANEL_NUM = -1 
                else
                    IS_ENTER_PRESSED = FALSE 
                end
            end
        end
    end
end

:CheatTypedCheck
15@v = "WALKSTYLE" 
if 
0AB1: call_scm_func @TestCheat 3 15@ 16@ 17@ 
then
    IS_CHEAT_TYPED = TRUE 
else
    IS_CHEAT_TYPED = FALSE 
end
return 

:TestCheat
{
   Originally made by:
        Seemann
}
// get 0@'s offset
0A9F: 4@ = current_thread_pointer
0A8E: 5@ = 4@ + 0xDC // mission Flag
0A8D: 5@ = read_memory 5@ size 1 virtual_protect 0 
if
    5@ == 1 
then
    4@ = 0xA48960 // mission locals
else
    4@ += 0x3C 
end
// get cheat string length
6@ = 0 
while true
    0A8D: 5@ = read_memory 4@ size 1 virtual_protect 0 
    if  and
        5@ > 0 
        6@ < 16 
    then
        4@ += 1 
        6@ += 1 
    else
        break
    end
end
8@ = 0x969110 // keypresses buffer
while 6@ > 0 
    4@ -= 1 
    0A8D: 5@ = read_memory 4@ size 1 virtual_protect 0 // last cheat char
    0A8D: 7@ = read_memory 8@ size 1 virtual_protect 0 // last pressed key
    8@ += 1 
    if
    803B:   5@ <> 7@ 
    then
        059A: return_false
        0AB2: ret 0 
    end
    6@ -= 1 
end
0A8C: write_memory 0x969110 size 1 value 0 virtual_protect 0 
0485: return_true
0AB2: ret 0 

:RemovePanel
0581: enable_radar 1 
01B4: set_player $PLAYER_CHAR can_move 1 
08DA: remove_panel MY_PANEL 
03E6: remove_text_box
return 

:DrawPanelMain
0512: show_permanent_text_box 'ANM1_1' // Use ~k~~GO_FORWARD~ and ~k~~GO_BACK~ to select a group.~N~~k~~PED_SPRINT~ Select~N~~k~~VEHICLE_ENTER_EXIT~ Exit
08D4: MY_PANEL = create_panel_with_title 'ANM1_3' position 29.0 145.0 width 93.0 columns 2 interactive 1 background 1 alignment 1 // Group List
08DB: set_panel MY_PANEL column 0 header 'ANM1_5' data 'ANM2_1' 'ANM2_2' 'ANM2_3' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' // Name // Man // Woman // Miscellaneous
08DB: set_panel MY_PANEL column 1 header 'ANM1_6' data 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' // Num
08EE: set_panel MY_PANEL column 1 row 0 text_1number GXT "NUMBER" number GROUP1_NUM 
08EE: set_panel MY_PANEL column 1 row 1 text_1number GXT "NUMBER" number GROUP2_NUM 
08EE: set_panel MY_PANEL column 1 row 2 text_1number GXT "NUMBER" number GROUP3_NUM 
08D6: set_panel MY_PANEL column 1 alignment 0 
return 

:GetPanelKeys
while true
    wait 0 
    if 
    00E1:   player 0 pressed_key 16 
    then
        repeat
            wait 0 
        until 80E1:   not player 0 pressed_key 16 
        IS_ENTER_PRESSED = FALSE 
        PANEL_NUM += 1 
        break
    else
       if 
       00E1:   player 0 pressed_key 15 
       then
            repeat
                wait 0 
            until 80E1:   not player 0 pressed_key 15 
            IS_ENTER_PRESSED = TRUE 
            PANEL_NUM -= 1 
            break
        end
    end
end
return 

:PanelCounter
0085: PANEL_MAX = GROUP_NUM 
0093: PANEL_MAX = integer PANEL_MAX to_float 
PANEL_MAX /= 10.0 
0087: 13@ = PANEL_MAX 
0092: 13@ = float 13@ to_integer 
0093: 13@ = integer 13@ to_float 
0087: 14@ = PANEL_MAX 
0092: PANEL_MAX = float PANEL_MAX to_integer 
0063: 14@ -= 13@ 
if 
    14@ > 0.0 
then
    PANEL_MAX += 1 
end
if 
    PANEL_MAX > 1 
then
    05A9: PANEL_ITEM[10] = 'ANM1_7' 
else
    05A9: PANEL_ITEM[10] = 'DUMMY' 
end
return 

:PanelGroup11
05A9: PANEL_ITEM[0] = 'ANM3_1' 
06D1: PANEL_ITEM_W[0] = "PLAYER" 
05A9: PANEL_ITEM[1] = 'ANM3_2' 
06D1: PANEL_ITEM_W[1] = "MAN" 
05A9: PANEL_ITEM[2] = 'ANM3_3' 
06D1: PANEL_ITEM_W[2] = "SHUFFLE" 
05A9: PANEL_ITEM[3] = 'ANM3_4' 
06D1: PANEL_ITEM_W[3] = "OLDMAN" 
05A9: PANEL_ITEM[4] = 'ANM3_5' 
06D1: PANEL_ITEM_W[4] = "GANG1" 
05A9: PANEL_ITEM[5] = 'ANM3_6' 
06D1: PANEL_ITEM_W[5] = "GANG2" 
05A9: PANEL_ITEM[6] = 'ANM3_7' 
06D1: PANEL_ITEM_W[6] = "OLDFATMAN" 
05A9: PANEL_ITEM[7] = 'ANM3_8' 
06D1: PANEL_ITEM_W[7] = "FATMAN" 
05A9: PANEL_ITEM[8] = 'ANM3_9' 
06D1: PANEL_ITEM_W[8] = "JOGGER" 
05A9: PANEL_ITEM[9] = 'ANM3_10' 
06D1: PANEL_ITEM_W[9] = "DRUNKMAN" 
return 

:PanelGroup12
05A9: PANEL_ITEM[0] = 'ANM3_11' 
06D1: PANEL_ITEM_W[0] = "BLINDMAN" 
05A9: PANEL_ITEM[1] = 'ANM3_12' 
06D1: PANEL_ITEM_W[1] = "SWAT" 
05A9: PANEL_ITEM[2] = 'DUMMY' 
05A9: PANEL_ITEM[3] = 'DUMMY' 
05A9: PANEL_ITEM[4] = 'DUMMY' 
05A9: PANEL_ITEM[5] = 'DUMMY' 
05A9: PANEL_ITEM[6] = 'DUMMY' 
05A9: PANEL_ITEM[7] = 'DUMMY' 
05A9: PANEL_ITEM[8] = 'DUMMY' 
05A9: PANEL_ITEM[9] = 'DUMMY' 
return 

:PanelGroup21
05A9: PANEL_ITEM[0] = 'ANM3_13' 
06D1: PANEL_ITEM_W[0] = "WOMAN" 
05A9: PANEL_ITEM[1] = 'ANM3_14' 
06D1: PANEL_ITEM_W[1] = "SHOPPING" 
05A9: PANEL_ITEM[2] = 'ANM3_15' 
06D1: PANEL_ITEM_W[2] = "BUSYWOMAN" 
05A9: PANEL_ITEM[3] = 'ANM3_16' 
06D1: PANEL_ITEM_W[3] = "SEXYWOMAN" 
05A9: PANEL_ITEM[4] = 'ANM3_17' 
06D1: PANEL_ITEM_W[4] = "PRO" 
05A9: PANEL_ITEM[5] = 'ANM3_18' 
06D1: PANEL_ITEM_W[5] = "OLDWOMAN" 
05A9: PANEL_ITEM[6] = 'ANM3_19' 
06D1: PANEL_ITEM_W[6] = "FATWOMAN" 
05A9: PANEL_ITEM[7] = 'ANM3_20' 
06D1: PANEL_ITEM_W[7] = "JOGWOMAN" 
05A9: PANEL_ITEM[8] = 'ANM3_21' 
06D1: PANEL_ITEM_W[8] = "OLDFATWOMAN" 
05A9: PANEL_ITEM[9] = 'DUMMY' 
return 

:PanelGroup31
05A9: PANEL_ITEM[0] = 'ANM3_22' 
gosub @LoadAnimSkateIFP 
06D1: PANEL_ITEM_W[0] = "SKATE" 
05A9: PANEL_ITEM[1] = 'DUMMY' 
05A9: PANEL_ITEM[2] = 'DUMMY' 
05A9: PANEL_ITEM[3] = 'DUMMY' 
05A9: PANEL_ITEM[4] = 'DUMMY' 
05A9: PANEL_ITEM[5] = 'DUMMY' 
05A9: PANEL_ITEM[6] = 'DUMMY' 
05A9: PANEL_ITEM[7] = 'DUMMY' 
05A9: PANEL_ITEM[8] = 'DUMMY' 
05A9: PANEL_ITEM[9] = 'DUMMY' 
return 

:LoadAnimSkateIFP
04ED: load_animation "SKATE" 
repeat
    wait 0 
until 04EE:   animation "SKATE" loaded
return 

:DrawPanelNext
0512: show_permanent_text_box 'ANM1_2' // Use ~k~~GO_FORWARD~ and ~k~~GO_BACK~ to select a walkstyle.~N~~k~~PED_SPRINT~ Select~N~~k~~VEHICLE_ENTER_EXIT~ Exit
08D4: MY_PANEL = create_panel_with_title 'ANM1_4' position 29.0 145.0 width 165.0 columns 1 interactive 1 background 1 alignment 1 // Anim Group
08DB: set_panel MY_PANEL column 0 header 'DUMMY' data PANEL_ITEM[0] PANEL_ITEM[1] PANEL_ITEM[2] PANEL_ITEM[3] PANEL_ITEM[4] PANEL_ITEM[5] PANEL_ITEM[6] PANEL_ITEM[7] PANEL_ITEM[8] PANEL_ITEM[9] 'DUMMY' PANEL_ITEM[10]
return 

:GetBaseAddress
0@ = 0x609A4E 
return 

:ReadFromMemory
gosub @GetBaseAddress 
0A8C: write_memory 0@ size 4 value 0x04D48689 virtual_protect 1 
0@ += 0x4 
0A8C: write_memory 0@ size 2 value 0x0 virtual_protect 1 
return 

:ReadFromPedsIde
gosub @GetBaseAddress 
0A8C: write_memory 0@ size 4 value 0x90909090 virtual_protect 1 
0@ += 0x4 
0A8C: write_memory 0@ size 2 value 0x9090 virtual_protect 1 
return 

:ScriptLock
0A9F: 0@ = current_thread_pointer 
000A: 0@ += 0x10 
0A8D: 0@ = read_memory 0@ size 4 virtual_protect 0 
000E: 0@ -= @Copyright 
return 

:Copyright
hex
   "Wesser © Copyright 2009/2010" 
   "Player Walkstyle Change (cleo)" 
   "Version 2.0 (final)" 
   "All right reserved." 
end
